# Maintainer: Nissar Chababy <contact at funilrys dot com>
# Ex-Maintainer: Max Falk <gmdfalk at gmail dot com>

pkgname=rocketchat-server
pkgver=8.1.0
pkgrel=1
pkgdesc="The Ultimate Open Source WebChat Platform"
arch=("any")
url="https://rocket.chat"
license=("MIT")
depends=("npm" "nodejs-lts-jod" "mongodb" "graphicsmagick" "curl" "python3" "gcc")
makedepends=()
optdepends=()
backup=("etc/rocketchat/${pkgname}.conf")
CFLAGS=-std=gnu11
options=("!strip")

install="${pkgname}.install"
source=("rocketchat-${pkgver}.tar::https://releases.rocket.chat/${pkgver}/download"
    "rocketchat-server.install"
    "rocketchat-server.conf"
    "rocketchat-user.conf"
"rocketchat.service")
sha512sums=('18b7654a4ea531611c22e18b547b1a77f10acbb1b120963a8d7e5a049742219be06e01a820a581865bb4530e7dfc7636fb38f84fb70a495ae31bf539b6c64521'
            '6700fae043f59881c0c8821db176a8d9cbbf7f047bd48750dbcd7abd7c531831436f910a28745b40d4d2fcbb7d2081b5512a5ee23ea6355bb065fde3b0672edc'
            '4ff8899a47612a81f73c1c6449fb30a7ddfb0b199756db7f73e0a3078cf818b88e481fd828296b148a348d137ae529ce591d6c2bd6b57ae9278188e715086b59'
            '0086f72f16a594116586d4b6783b104f7bba779e4f8e31e5988c7fa67e1c7d9fc95215d0a04c4f24c72b4183774a9768a29b05c828990125dd4a3379a69aa648'
            'b77e49bfb919c781170fd485658a44646f30d54dc724216d265e82f06e340a50eeda1a8ee0b3dc3e364e4f9ab41e684ebb1e59cc54c90c335b800a51dceebf8f')

package() {
    install -dm755 "${pkgdir}/usr/share/${pkgname}"
    cp -dr --preserve=mode --no-preserve=ownership ${srcdir}/bundle/* "${pkgdir}/usr/share/${pkgname}"

    pushd "${pkgdir}/usr/share/${pkgname}/programs/server"
    npm install --cache "${srcdir}/npm-cache"
    popd

    install -Dm644 ${pkgname}.conf "${pkgdir}/etc/rocketchat/${pkgname}.conf"
    install -Dm644 rocketchat.service "${pkgdir}/usr/lib/systemd/system/rocketchat.service"
    install -Dm644 rocketchat-user.conf "${pkgdir}/usr/lib/sysusers.d/rocketchat.conf"
}
